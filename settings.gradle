pluginManagement {
    repositories {
        gradlePluginPortal()

        maven {
            name = "Forge"
            url = "https://maven.minecraftforge.net/"
        }
        maven {
            name = "Fabric"
            url = "https://maven.fabricmc.net/"
        }
        maven {
            name = "Sponge Snapshots"
            url = "https://repo.spongepowered.org/repository/maven-public/"
        }
        maven {
            name = "Parchment MC"
            url = "https://maven.parchmentmc.org"
        }
    }
}

plugins {
    id("org.gradle.toolchains.foojay-resolver-convention") version("0.7.0")
}

rootProject.name = "Infamous-Libraries"

include("common")
include("fabric")
include("forge")

for (File file in getFile("modules").listFiles()) {
    if (isModuleDir(file)) {
        String name = file.name
        include(name)
        ProjectDescriptor project = project(":" + name)
        project.projectDir = getFile("modules/" + name)
    }
}

def getFile(String name) {
    return file(name)
}

static def isModuleDir(File file) {
    if (file.isDirectory()) {
        File[] files = file.listFiles()
        if (files != null) {
            for (File subFile in files) {
                if (subFile.name == "build.gradle") {
                    return true
                }
            }
        }
    }
    return false
}